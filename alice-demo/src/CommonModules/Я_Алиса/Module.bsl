
Функция ОтветАлисе(ОбработанныйЗапрос) Экспорт
	
	СостояниеСессии = Новый Структура;
	ДанныеОтвета = Неопределено;
	
	Если ОбработанныйЗапрос.Команда = "" Тогда
		ДанныеОтвета = ОтветПерваяФраза();
	Иначе
		Я_АлисаПереопределяемый.ПриФормированииОтвета(ДанныеОтвета, СостояниеСессии,
			ОбработанныйЗапрос.Команда, 
			ОбработанныйЗапрос.СостояниеСессии);
	КонецЕсли;
	
	Если ДанныеОтвета = Неопределено Тогда
		ДанныеОтвета = ОтветНеизвестнаяКоманда();
	КонецЕсли;
	
	Ответ = Новый Структура;
	Ответ.Вставить("response", ДанныеОтвета);
	Ответ.Вставить("session_state", СостояниеСессии);
	Ответ.Вставить("version", "1.0");
	
	Возврат Я_ОбработкаJSON.ОбъектВJSON(Ответ);
	
КонецФункции

Функция Кнопка(Текст) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("title", Текст);
	Результат.Вставить("hide", Истина);
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеОтвета(Текст, Кнопки = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("text", Текст);
	Результат.Вставить("end_session", Ложь);
	
	Если Кнопки <> Неопределено Тогда
		Результат.Вставить("buttons", Кнопки);
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция СтандартныеКнопки() Экспорт
		
	Кнопки = Новый Массив;
	Кнопки.Добавить(Кнопка("Сообщи показатель"));
	Кнопки.Добавить(Кнопка("Отправь отчет"));

	Возврат Кнопки;
	
КонецФункции

Функция НовоеСостояниеСессии(Сценарий) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Сценарий", Сценарий);
	
	Возврат Результат;
	
КонецФункции

Функция ОтветНеизвестнаяКоманда()   
	
	ТекстОтвета = "Я не знаю такой команды. Я могу повторить фразу, написать в телеграм, сообщить показатель или отправить отчет";
	
	Возврат ДанныеОтвета(ТекстОтвета, СтандартныеКнопки());
	
КонецФункции

Функция ОтветПерваяФраза()
	
	ТекстОтвета = "Привет с Желтой субмарины. Я могу повторить фразу, написать в телеграм, сообщить показатель или отправить отчет";
	
	Возврат ДанныеОтвета(ТекстОтвета, СтандартныеКнопки());
	
КонецФункции
